#pragma kernel TrashcoreYCbCr
#pragma kernel TrashcoreCronch
#pragma kernel TrashcoreDct
#pragma kernel TrashcoreCrunch
#pragma kernel TrashcoreIdct
#pragma kernel TrashcoreOutput

// Create a RenderTexture with enableRandomWrite flag and set it
RWTexture2D<float4> Result;
Texture2D<float4> Input;
SamplerState samplerInput; // auto-gemerated by unity if name is texture plus "sampler" prefix 
Texture2D<float4> Luma; // used only in the output shader
Texture2D<float4> Chroma; // used only in the output shader
Texture2D<float4> Original; // used only in TrashcoreIdct

float Resolution_x;
float Resolution_y;
float crunch_levels;
int juice;
int input_size_x;
int input_size_y;
int output_size_x;
int output_size_y;
int cronch;

float4 RGBToYCbCr(float4 rgba)
{
    float Y  = 0.299 * rgba.r + 0.587 * rgba.g + 0.114 * rgba.b;
    float Cb = (rgba.b - Y) * 0.564 + 0.5;
    float Cr = (rgba.r - Y) * 0.713 + 0.5;
    return float4(Cr, Y, Cb, 1.0); // not typical order but is closest to rgb while debugging
}

[numthreads(8,8,1)]
void TrashcoreYCbCr (uint3 id : SV_DispatchThreadID)
{
    float x = id.x / Resolution_x;
    float y = id.y / Resolution_y;
    float4 inputColor = Input.SampleLevel(samplerInput, float2(x, y), 0);
    Result[id.xy] = RGBToYCbCr(inputColor);
}

[numthreads(8,8,1)]
void TrashcoreCronch (uint3 id : SV_DispatchThreadID)
{
    float4 tap_00 = Input.Load(int3(    2 * id.x,     2 * id.y, 0));
    float4 tap_01 = Input.Load(int3(1 + 2 * id.x,     2 * id.y, 0));
    float4 tap_02 = Input.Load(int3(    2 * id.x, 1 + 2 * id.y, 0));
    float4 tap_03 = Input.Load(int3(1 + 2 * id.x, 1 + 2 * id.y, 0));
    Result[id.xy] = 0.25 * (tap_00 + tap_01 + tap_02 + tap_03);
}

[numthreads(8,8,1)]
void TrashcoreDct (uint3 id : SV_DispatchThreadID)
{
    int frequency_limit = 6 + juice;
    int offset_x = 4 * (uint(id.y) % 2);
    float offset_xf = float(offset_x);
    float input_size_xf = float(input_size_x);
    float input_size_yf = float(input_size_y);

    // ssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssssss
    for (int s = 0; s < frequency_limit; s++) // s and t are in frequency space (output)
    {
        float C_s = (s == 0) ? sqrt(0.5) : 1.0;
        int x_out = id.x * 8 + offset_x + s;
        if (x_out >= output_size_x) x_out -= output_size_x;
        
        // tttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttt
        for (int t = 0; t < frequency_limit; t++)
        {
            float C_t = (t == 0) ? sqrt(0.5) : 1.0;
            int y_out = id.y * 8 + t;
            float4 sum = 0.0;

            // uuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuu
            for (float u = 0.0; u < 8.0; u += 1.0)  // u and v are in texture space (input
            {
                float cos_x = cos(((2.0 * u + 1.0) * float(s) * 3.14159265359) / 16.0);
                float x_in = float(id.x) * 8.0 + offset_xf + u;
                if (x_in >= input_size_xf) x_in = 0.0;
                // vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
                for (float v = 0.0; v < 8.0; v += 1.0)
                {
                    float cos_y = cos(((2.0 * v + 1.0) * float(t) * 3.14159265359) / 16.0);
                    float y_in = float(id.y) * 8.0 + v;
                    float2 uv = float2(x_in / input_size_xf, y_in / input_size_yf);
                    float4 pixelValue = Input.SampleLevel(samplerInput, uv, cronch);
                    sum += pixelValue * cos_x * cos_y;
                }
            }

            sum.w = 1.0;

            Result[int2(x_out, y_out)] = 0.25 * C_s * C_t * sum;
        }
    }
}

[numthreads(8,8,1)]
void TrashcoreCrunch (uint3 id : SV_DispatchThreadID)
{
    float4 inputValue = Input.Load(int3(id.x, id.y, 0));

    // Reduce total number of posterization levels as one gets further from DCT block origin
    int2 dct_offset = int2(id.x % 8, id.y % 8);
    float manhattanDistance = dct_offset.x + dct_offset.y; // oh yeah cuz we nasty like that
    float t = max(0.0, (manhattanDistance - 5.0)) / (16.0 - 5.0);
    //float frequency_falloff = lerp(1.0, 4.0, t);
    float frequency_falloff = 1.0;
    // actual jpeg compression uses a different formula but lets exeriment a little
    Result[id.xy] = floor(inputValue * crunch_levels / frequency_falloff) / crunch_levels * frequency_falloff;
}

[numthreads(8,8,1)]
void TrashcoreIdct (uint3 id : SV_DispatchThreadID)
{
    // id.x and id.y represent the IDCT block indices (not pixels!)
    uint x_max = int(Resolution_x);
    int offset_x = (id.y % 2 == 0) ? 0 : 4;
    int origin_x = (id.x * 8 + offset_x) % x_max;
    int2 idct_origin = int2(origin_x, id.y * 8);
    float4 sum = float4(0.0, 0.0, 0.0, 1.0);
    float4 tap0 = float4(0.0, 0.0, 0.0, 1.0);
    float4 tap1 = float4(0.0, 0.0, 0.0, 1.0);
    int frequency_limit = 6 + juice;
    
    for (int u = 0; u < 8; u++) // u and v are in texture space (output)
    {
        uint x_out = uint(idct_origin.x + u) % x_max;
        for (int v = 0; v < 8; v++)
        {
            float4 result = float4(0.0, 0.0, 0.0, 1.0);
            for (int s = 0; s < frequency_limit; s++) // s and t are in frequency space (input)
            {
                uint x_in = uint(idct_origin.x + s) % x_max;
                for (int t = 0; t < frequency_limit; t++)
                {
                    float4 crunchedCoeficient = Input.Load(int3(x_in, idct_origin.y + t, 0));
                    float cos_x = cos(((2 * u + 1) * s * 3.14159265359) / 16.0);
                    float cos_y = cos(((2 * v + 1) * t * 3.14159265359) / 16.0);
                    float C_s = (s == 0) ? sqrt(0.5) : 1.0;
                    float C_t = (t == 0) ? sqrt(0.5) : 1.0;
                    // IDCT formula
                    result += crunchedCoeficient * cos_x * cos_y * C_s * C_t;
                }
            }
            // scale the result by 0.25 (per IDCT formula)
            result *= 0.25;
            // write the IDCT value to the output texture
            
            Result[int2(x_out, idct_origin.y + v)] = result;
        }
    }
    // bandaid fix. the first pixel looks very green for some reason.
    // first attempt was to cache nearby values but it was still off.
    //    - Result[int2(idct_origin.x, idct_origin.y)] = 0.35 * tap0 + 0.65 * tap1;
    // after trying ahandful of different cached pixel value gave up. lets just read the root pixel for each dct block
    // from the original
    int2 origin_in_original = int2(int(1024.0 / Resolution_x) * idct_origin);
    Result[int2(idct_origin.x, idct_origin.y)] = Original.Load(int3(origin_in_original, 0));
}

[numthreads(8,8,1)]
void TrashcoreOutput (uint3 id : SV_DispatchThreadID)
{
    int chroma_x = id.x * int(Resolution_x) / 1024;
    int chroma_y = id.y * int(Resolution_y) / 512;
    float4 chroma = Chroma.Load(int3(chroma_x, chroma_y, 0));
    float4 luma = Luma.Load(int3(id.xy, 0));
    
    float y = luma.y; // ycbcr is encoded in cr, y, cb order in rgb space 
    float cb = chroma.b;
    float cr = chroma.r;

    float r = y + 1.402 * (cr - 0.5);
    float g = y - 0.344136 * (cr - 0.5) - 0.714136 * (cb - 0.5);
    float b = y + 1.772 * (cb - 0.5);
    Result[id.xy] = float4(r, g, b, 1.0);
}